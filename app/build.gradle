buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

def Properties keyProps = new Properties()
keyProps.load(new FileInputStream(file('../keystore.properties')))

def CRASHLYTICS_KEY = keyProps["crashlytics"];

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 20
        versionCode 8
        versionName "1.8"
    }

    signingConfigs {
        release {
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [CRASHLYTICS_KEY: CRASHLYTICS_KEY]
            buildConfigField 'String', 'AD_UNIT_ID','"' + keyProps["adUnitId"] + '"'
            buildConfigField 'String', 'AD_TEST_DEVICE_ID','"' + keyProps["adTestDeviceId"] + '"'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFile 'proguard-rules.pro'
            manifestPlaceholders = [CRASHLYTICS_KEY: CRASHLYTICS_KEY]
            buildConfigField 'String', 'AD_UNIT_ID','"' + keyProps["adUnitId"] + '"'
            buildConfigField 'String', 'AD_TEST_DEVICE_ID','"' + keyProps["adTestDeviceId"] + '"'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

}

dependencies {
    compile 'com.android.support:appcompat-v7:20.+'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.google.android.gms:play-services-ads:6.5.+'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.crashlytics.android:crashlytics:1.+'
}
